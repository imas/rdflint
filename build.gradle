plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'com.github.spotbugs' version '4.0.5'
}
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
    mavenCentral()
}
dependencies {
    compile 'org.apache.jena:jena-arq:3.9.0'
    compile 'commons-cli:commons-cli:1.4'
    compile 'org.jline:jline:3.9.0'
    compile 'org.yaml:snakeyaml:1.23'
    compile 'org.codehaus.groovy:groovy:3.0.0-rc-3'
    compile 'org.slf4j:slf4j-log4j12:1.7.25'
    compile 'org.thymeleaf:thymeleaf:3.0.11.RELEASE'
    compile 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.2.2'
    compile 'org.reflections:reflections:0.9.11'
    compile 'com.github.albfernandez:juniversalchardet:2.3.0'
    compile 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.8.1'
    compile group: 'org.topbraid', name: 'shacl', version: '1.3.0'

    compileOnly 'net.jcip:jcip-annotations:1.0'
    compileOnly 'com.github.spotbugs:spotbugs:4.0.2'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.0.2'

    testCompile 'junit:junit:4.+'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:3.1.0'
}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
jar {
    manifest {
        attributes 'Main-Class': 'com.github.imas.rdflint.RdfLint'
    }
}
artifacts {
    archives shadowJar
}

checkstyle {
    toolVersion '8.23'
}
pmd {
    toolVersion '6.22.0'
    sourceSets = [sourceSets.main]
    ruleSets = [
            "category/java/bestpractices.xml",
            "category/java/multithreading.xml",
            "category/java/performance.xml",
            "category/java/security.xml"
    ]
}
spotbugs {
    toolVersion = '4.0.2'
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
build.dependsOn jacocoTestReport, jacocoTestCoverageVerification
jacocoTestCoverageVerification.mustRunAfter jacocoTestReport
