plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    // id 'com.github.spotbugs' version '6.0.27'
}
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}
dependencies {
    implementation 'org.apache.jena:jena-arq:4.7.0'
    implementation 'commons-cli:commons-cli:1.9.0'
    implementation 'org.jline:jline:3.28.0'
    implementation 'org.yaml:snakeyaml:2.3'
    implementation 'org.apache.groovy:groovy:4.0.24'
    implementation 'org.slf4j:slf4j-log4j12:2.0.16'
    implementation 'org.thymeleaf:thymeleaf:3.1.3.RELEASE'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.3.0'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'com.github.albfernandez:juniversalchardet:2.3.0'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.23.1'
    implementation 'org.topbraid:shacl:1.3.0'

    compileOnly 'net.jcip:jcip-annotations:1.0'
    compileOnly 'com.github.spotbugs:spotbugs:4.8.6'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.8.6'

    testImplementation 'junit:junit:4.+'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-core:3.1.0'
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
jar {
    manifest {
        attributes 'Main-Class': 'com.github.imas.rdflint.RdfLint'
    }
}
artifacts {
    archives shadowJar
}

checkstyle {
    toolVersion '8.23'
}
pmd {
    toolVersion '6.22.0'
    sourceSets = [sourceSets.main]
    ruleSets = [
            "category/java/bestpractices.xml",
            "category/java/multithreading.xml",
            "category/java/performance.xml",
            "category/java/security.xml"
    ]
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}
checkstyleMain {
    reports {
        xml.required = false
        html.required = true
    }
}
build.dependsOn jacocoTestReport, jacocoTestCoverageVerification
jacocoTestCoverageVerification.mustRunAfter jacocoTestReport
